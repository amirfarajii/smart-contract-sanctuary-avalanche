// SPDX-License-Identifier: MIT
pragma solidity ^0.8.12;

import "../../integrations/gmx/IVault.sol";
import "../../integrations/gmx/IRewardRouterV2.sol";
import "../../integrations/gmx/IGlpManager.sol";
import "../BaseBuildingBlock.sol";
import "./Guarded.sol";

 /**
  * @author DeCommas team
  * @title GMX DEX interface
  */
contract GmxVaultStage is Guarded {

    IVault public gmxVault;
    IRewardRouterV2 public rewardRouter;
    IGlpManager public glpManager;
    address public glpToken;
    address public stakedGlpTrackerToken;
    uint256 public constant PRICE_PRECISION = 10 ** 30;
    uint256 public constant USDG_DECIMALS = 18;
    address public owner;
    
    // asset => depositor => balance
    mapping(address => mapping(address => uint256)) private _deposits;
    
    event DepositEvent(address depositor, address baseAsset, uint256 amount);
    event BuyingEvent(address _token,uint256 _amount,uint256 glpAmountReceived);
    event SellingEvent(address _receiver, uint256 _glpAmount,address _tokenOut,uint256 _amountReceived);
    
    modifier onlyOwner(){
        require(msg.sender == owner, "OnlyOwner");
        _;
    }
    constructor(){ owner = msg.sender;}

    function initialize (
        IVault gmxVault_,
        IRewardRouterV2 rewardRouter_
    ) external onlyOwner {
        __RelyOnSender();
        gmxVault = IVault(gmxVault_);
        rewardRouter = IRewardRouterV2(rewardRouter_);
        glpManager = IGlpManager(rewardRouter.glpManager());
        glpToken = rewardRouter.glp();
        stakedGlpTrackerToken = rewardRouter.stakedGlpTracker();
    }

    function deposit(
        address baseAsset,
        uint256 amount,
        address depositor
    ) external payable {
        if (baseAsset == address(0x0)) {
            require(
                msg.value == amount,
                "GMX BB: message value mismatch amount"
            );
        } else {
            require(
                IERC20(baseAsset).transferFrom(
                    depositor,
                    address(this),
                    amount
                ),
                "GMX BB: Desposit transfer failed"
            );
        }
        emit DepositEvent(depositor, baseAsset, amount);
    }

    /**
    * @notice buy GLP, mint and stake
    * @dev _token : the token to buy GLP with
    * @dev _amount : the amount of token to use for the purchase
    * @dev _minUsdg : the minimum acceptable USD value of the GLP purchased // do we calculate on chain or off?
    * @dev _minGlp : the minimum acceptable GLP amount
    * @dev Rewards router and GLP manager spent must be approved before
    * @dev stakedGlpTrackerToken is 1:1 ratio with glp token
    * @dev send address Zero to buy with native currency
    * @return glpBoughtAmount bought amount
    * */
    function buyGLP(address _token, uint256 _amount, uint256 _minUsdg, uint256 _minGlp) external auth returns(uint256 glpBoughtAmount){
    if (_token == address(0x0)) {
        require(address(this).balance >= _amount,"GMX BB: Bridge or deposit native currency");
        glpBoughtAmount = rewardRouter.mintAndStakeGlpETH{value: _amount}(_minUsdg, _minGlp);
    }else{
        require(_amount > 0,"GMX BB: Zero amount");

        // check for balance to buy with
        require(IERC20(_token).balanceOf(address(this)) >= _amount,"GMX BB: Bridge or deposit assets.");

        // approve to void contracts is necessary
        IERC20(_token).approve(address(rewardRouter), _amount);
        IERC20(_token).approve(address(glpManager), _amount);

        // get glp balance after buying
        uint256 glpBalanceBefore = IERC20(stakedGlpTrackerToken).balanceOf(address(this));

        // buy glp
        glpBoughtAmount = rewardRouter.mintAndStakeGlp(
            _token, // the token to buy GLP with
            _amount, // the amount of token to use for the purchase
            _minUsdg, // the minimum acceptable USD value of the GLP purchased
            _minGlp  // minimum acceptable GLP amount
        );

        // check glp balance after buying
        uint256 glpBalanceAfter = IERC20(stakedGlpTrackerToken).balanceOf(address(this));

        // get glp balance change after buy
        uint256 _balanceChange = glpBalanceAfter - glpBalanceBefore;

        // check if contract balance reflects the buying
        require(_balanceChange >= glpBoughtAmount, "GMX BB: Glp buying failed. ");
    }

        emit BuyingEvent(_token,_amount,glpBoughtAmount);
        return glpBoughtAmount;
    }

    /**
    *   @notice Sell / unstake and redeem GLP
    *   @dev _tokenOut : the token to sell GLP for
    *   @dev _glpAmount : the amount of GLP to sell
    *   @dev _minOut : the minimum acceptable amount of tokenOut to be received
    *   @return _amountPayed payed for the sell
    * */
    function sellGLP(address _tokenOut, uint256 _glpAmount, uint256 _minOut) external auth returns (uint256 _amountPayed){
       if(_tokenOut == address(0x0)){
            _amountPayed = rewardRouter.unstakeAndRedeemGlpETH(_glpAmount, _minOut, payable(address(this)));
       }else{
            // get contract balance before selling
            uint256 _tokenOutBalanceBefore = IERC20(_tokenOut).balanceOf(address(this));

            // sell glp
            _amountPayed = rewardRouter.unstakeAndRedeemGlp(_tokenOut, _glpAmount, _minOut,address(this));

            // get contract balance after selling
            uint256 _tokenOutBalanceAfter = IERC20(_tokenOut).balanceOf(address(this));

            // get balance change
            uint256 _balanceChange = _tokenOutBalanceAfter - _tokenOutBalanceBefore;

            // check if vault balance reflects the sale
            require(_balanceChange >=_amountPayed, "GMX BB: Glp buying failed. ");
       }

        emit SellingEvent(msg.sender, _glpAmount, _tokenOut,_amountPayed);
        return _amountPayed;
    }

    function withdraw(address _token, uint256 _amount, address payable _receiver) external auth returns(bool){
        if(_token == address(0x0)){
            require(address(this).balance >= _amount, "GMX BB: Insufficient funds");
            address(_receiver).call{value:_amount}("");
        }else{
            require(IERC20(_token).balanceOf(address(this)) >= _amount, "GMX BB: Insufficient funds");
            require(IERC20(_token).transfer(_receiver,_amount),"GMX BB: Withdraw failed");
        }
    }

    /**
     *  @notice Trigger rewards compounding and claims them
     *  @dev _shouldClaimGmx boolean yes/no
     *  @dev _shouldStakeGmx boolean yes/no
     *  @dev _shouldClaimEsGmx boolean yes/no
     *  @dev _shouldStakeEsGmx boolean yes/no
     *  @dev _shouldStakeMultiplierPoints boolean yes/no
     *  @dev _shouldClaimWeth boolean yes/no
     *  @dev _shouldConvertWethToEth boolean yes/no
     *  @dev 15 avrg min cooldown time
     */
    function claimRewards(
        bool _shouldClaimGmx,
        bool _shouldStakeGmx,
        bool _shouldClaimEsGmx,
        bool _shouldStakeEsGmx,
        bool _shouldStakeMultiplierPoints,
        bool _shouldClaimWeth,
        bool _shouldConvertWethToEth
    ) external auth returns(bool){
            rewardRouter.handleRewards(
                    _shouldClaimGmx,
                    _shouldStakeGmx,
                    _shouldClaimEsGmx,
                    _shouldStakeEsGmx,
                    _shouldStakeMultiplierPoints,
                    _shouldClaimWeth,
                    _shouldConvertWethToEth
                );
            return true;
    }

    function getTvl() public returns(uint256){
        uint256 glpPrie = glpManager.getAum(true) / IERC20(glpToken).totalSupply();
        uint256 fsGlpAmount = IERC20(stakedGlpTrackerToken).balanceOf(address(this));
        return fsGlpAmount * glpPrie;
    }
    /*
        getAUms USG
        glp total supply
     */

    /**
    * @notice Get the list of tokens with addresses
    */
    function usdgAmounts(address _token) public view returns (uint256){
        return gmxVault.usdgAmounts(_token);
    }
    /**
     * @notice Get GLP price depending on opened positions
     * @param _maximize: 
     * @dev Due to possible spread on token price:
     * @dev true returns the maximum worth of tokens at that point in time.
     * @dev false returns the minimum worth.
     * @dev true for buyingPrice & false for selling price
     */
    function getGlpPrice(bool _maximize) public view returns(uint256){
        return glpManager.getAumInUsdg(_maximize) / IERC20(glpManager.glp()).totalSupply();
    }

   function globalShortAveragePrices(address _token) public view returns(uint256){
        return gmxVault.globalShortAveragePrices(_token) / 1e12;
    }

    /**
     * @notice retrieve the reserved amount of token in vault
     */
    function getReservedAmounts(address _asset) public view returns(uint256){
        return gmxVault.reservedAmounts(_asset);
    }

    function getGLPBalance() public view returns(uint256){
        return IERC20(stakedGlpTrackerToken).balanceOf(address(this));
    }

    function getGlpTotalSupply() public view returns(uint256){
        return IERC20(glpToken).totalSupply();
    }

    function getPoolSize(address _asset) public view returns(uint256){
        return gmxVault.poolAmounts(_asset);
    }

    /**
    * @notice Get Assets under management in USDg
    */
    function getAumInUsdg(bool maximise) public view returns (uint256) {
         return glpManager.getAumInUsdg(maximise);
    }

    function getAssetBalance(address _asset) public view returns(uint256){
        return IERC20(_asset).balanceOf(address(this));
    }
    
    receive() external payable {
        revert();
    }

    fallback() external payable {
        revert();
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.12;

import "../SmartLZBase.sol";

abstract contract BaseBuildingBlock is SmartLZBase {

    address public nativeRouter;

    event TransferredToNativeRouter(address token, uint256 amount);

    function lzReceive(
        uint16 srcChainId,
        bytes memory srcAddress,
        uint64, /*_nonce*/
        bytes memory payload
    ) external override {
      //  require(_msgSender() == address(_nativeLZEndpoint), "BBB: Sender isn't lzEndpoint");
      //  require( //      srcAddress.length == remotes[srcChainId].length
      //      && keccak256(srcAddress) == keccak256(remotes[srcChainId]),
      //      "BBB:Invalid remote,call setRemote(srcAddr)" //  );
        (uint16 flag, bytes memory actionData) = abi.decode(payload,(uint16, bytes));
        if(flag == 1) { // bridge
            _bridge(actionData);
        } else if(flag == 2) { // action
            (bool success, bytes memory returnData) = address(this).call(actionData);
            require(success, "BBB:call to destination bb failed");
        }
        else if(flag == 6) { // set bridge
            _setBridge(actionData);
        }
        else {
            revert("BBB:unsupported flag type");
        }
    }


    function approve(bytes memory _data) public /* onlySelf() */ {
        (address baseAsset, address spender, uint256 amount) = abi.decode(_data, (address, address, uint256));
        IERC20(baseAsset).approve(spender, amount);
    }

    function backTokensToNative(bytes memory _data) public /* onlySelf() */ {
        (address _token, uint256 amount) = abi.decode(_data, (address, uint256));
        IERC20(_token).transfer(nativeRouter, amount);
        emit TransferredToNativeRouter(_token, amount);
    }


    function nativeBridge(bytes memory actionData) external {
        require(_msgSender() == _actionPool, "BBB:sender isn't actionPool");
        _bridge(actionData);
    }


    // todo only ActionPool
    function setNativeRouter(address _new) public /* onlySelf() */ {
        // (address _new) = abi.decode(_data, (address));
        address oldNativeRouter = nativeRouter;
        nativeRouter = _new;
        emit RouterChanged(_msgSender(), oldNativeRouter, nativeRouter);
    }
}

// SPDX-License-Identifier: MIT

pragma solidity 0.8.12;
interface IGlpManager {
    function usdg() external view returns (address);
    function glp() external view returns (address);
    function cooldownDuration() external returns (uint256);
    function getAum(bool maximise) external payable returns (uint256);
    function getAums() external returns (uint256[] memory);
    function getAumInUsdg(bool maximise) external view returns (uint256);
    function lastAddedAt(address _account) external returns (uint256);
    function addLiquidity(address _token, uint256 _amount, uint256 _minUsdg, uint256 _minGlp) external returns (uint256);
    function addLiquidityForAccount(
        address _fundingAccount,
        address _account,
        address _token,
        uint256 _amount,
        uint256 _minUsdg,
        uint256 _minGlp
    ) external returns (uint256);
    function removeLiquidity(address _tokenOut, uint256 _glpAmount, uint256 _minOut, address _receiver) external returns (uint256);
    function removeLiquidityForAccount(
        address _account,
        address _tokenOut,
        uint256 _glpAmount,
        uint256 _minOut,
        address _receiver
    ) external returns (uint256);
}

// SPDX-License-Identifier: MIT

pragma solidity 0.8.12;

interface IVaultUtils {
    function updateCumulativeFundingRate(address _collateralToken, address _indexToken) external returns (bool);
    function validateIncreasePosition(address _account, address _collateralToken, address _indexToken, uint256 _sizeDelta, bool _isLong) external ;
    function validateDecreasePosition(address _account, address _collateralToken, address _indexToken, uint256 _collateralDelta, uint256 _sizeDelta, bool _isLong, address _receiver) external ;
    function validateLiquidation(address _account, address _collateralToken, address _indexToken, bool _isLong, bool _raise) external  returns (uint256, uint256);
    function getEntryFundingRate(address _collateralToken, address _indexToken, bool _isLong) external  returns (uint256);
    function getPositionFee(address _account, address _collateralToken, address _indexToken, bool _isLong, uint256 _sizeDelta) external  returns (uint256);
    function getFundingFee(address _account, address _collateralToken, address _indexToken, bool _isLong, uint256 _size, uint256 _entryFundingRate) external  returns (uint256);
    function getBuyUsdgFeeBasisPoints(address _token, uint256 _usdgAmount) external  returns (uint256);
    function getSellUsdgFeeBasisPoints(address _token, uint256 _usdgAmount) external  returns (uint256);
    function getSwapFeeBasisPoints(address _tokenIn, address _tokenOut, uint256 _usdgAmount) external  returns (uint256);
    function getFeeBasisPoints(address _token, uint256 _usdgDelta, uint256 _feeBasisPoints, uint256 _taxBasisPoints, bool _increment) external  returns (uint256);
}

interface IVault {
    function isInitialized() external view returns (bool);
    function isSwapEnabled() external view returns (bool);
    function isLeverageEnabled() external view returns (bool);

    function setVaultUtils(IVaultUtils _vaultUtils) external;
    function setError(uint256 _errorCode, string calldata _error) external;

    function router() external view returns (address);
    function usdg() external view returns (address);
    function gov() external view returns (address);

    function whitelistedTokenCount() external view returns (uint256);
    function maxLeverage() external view returns (uint256);

    function minProfitTime() external view returns (uint256);
    function hasDynamicFees() external view returns (bool);
    function fundingInterval() external view returns (uint256);
    function totalTokenWeights() external view returns (uint256);
    function getTargetUsdgAmount(address _token) external view returns (uint256);

    function inManagerMode() external view returns (bool);
    function inPrivateLiquidationMode() external view returns (bool);

    function maxGasPrice() external view returns (uint256);

    function approvedRouters(address _account, address _router) external view returns (bool);
    function isLiquidator(address _account) external view returns (bool);
    function isManager(address _account) external view returns (bool);

    function minProfitBasisPoints(address _token) external view returns (uint256);
    function tokenBalances(address _token) external view returns (uint256);
    function lastFundingTimes(address _token) external view returns (uint256);

    function setMaxLeverage(uint256 _maxLeverage) external;
    function setInManagerMode(bool _inManagerMode) external;
    function setManager(address _manager, bool _isManager) external;
    function setIsSwapEnabled(bool _isSwapEnabled) external;
    function setIsLeverageEnabled(bool _isLeverageEnabled) external;
    function setMaxGasPrice(uint256 _maxGasPrice) external;
    function setUsdgAmount(address _token, uint256 _amount) external;
    function setBufferAmount(address _token, uint256 _amount) external;
    function setMaxGlobalShortSize(address _token, uint256 _amount) external;
    function setInPrivateLiquidationMode(bool _inPrivateLiquidationMode) external;
    function setLiquidator(address _liquidator, bool _isActive) external;

    function setFundingRate(uint256 _fundingInterval, uint256 _fundingRateFactor, uint256 _stableFundingRateFactor) external;

    function setFees(
        uint256 _taxBasisPoints,
        uint256 _stableTaxBasisPoints,
        uint256 _mintBurnFeeBasisPoints,
        uint256 _swapFeeBasisPoints,
        uint256 _stableSwapFeeBasisPoints,
        uint256 _marginFeeBasisPoints,
        uint256 _liquidationFeeUsd,
        uint256 _minProfitTime,
        bool _hasDynamicFees
    ) external;

    function setTokenConfig(
        address _token,
        uint256 _tokenDecimals,
        uint256 _redemptionBps,
        uint256 _minProfitBps,
        uint256 _maxUsdgAmount,
        bool _isStable,
        bool _isShortable
    ) external;

    function setPriceFeed(address _priceFeed) external;
    function withdrawFees(address _token, address _receiver) external returns (uint256);

    function directPoolDeposit(address _token) external;
    function buyUSDG(address _token, address _receiver) external returns (uint256);
    function sellUSDG(address _token, address _receiver) external returns (uint256);
    function swap(address _tokenIn, address _tokenOut, address _receiver) external returns (uint256);
    function increasePosition(address _account, address _collateralToken, address _indexToken, uint256 _sizeDelta, bool _isLong) external;
    function decreasePosition(address _account, address _collateralToken, address _indexToken, uint256 _collateralDelta, uint256 _sizeDelta, bool _isLong, address _receiver) external returns (uint256);
    function liquidatePosition(address _account, address _collateralToken, address _indexToken, bool _isLong, address _feeReceiver) external;
    function tokenToUsdMin(address _token, uint256 _tokenAmount) external view returns (uint256);

    function priceFeed() external view returns (address);
    function fundingRateFactor() external view returns (uint256);
    function stableFundingRateFactor() external view returns (uint256);
    function cumulativeFundingRates(address _token) external view returns (uint256);
    function getNextFundingRate(address _token) external view returns (uint256);
    function getFeeBasisPoints(address _token, uint256 _usdgDelta, uint256 _feeBasisPoints, uint256 _taxBasisPoints, bool _increment) external view returns (uint256);

    function liquidationFeeUsd() external view returns (uint256);
    function taxBasisPoints() external view returns (uint256);
    function stableTaxBasisPoints() external view returns (uint256);
    function mintBurnFeeBasisPoints() external view returns (uint256);
    function swapFeeBasisPoints() external view returns (uint256);
    function stableSwapFeeBasisPoints() external view returns (uint256);
    function marginFeeBasisPoints() external view returns (uint256);

    function allWhitelistedTokensLength() external view returns (uint256);
    function allWhitelistedTokens(uint256) external view returns (address);
    function whitelistedTokens(address _token) external view returns (bool);
    function stableTokens(address _token) external view returns (bool);
    function shortableTokens(address _token) external view returns (bool);
    function feeReserves(address _token) external view returns (uint256);
    function globalShortSizes(address _token) external view returns (uint256);
    function globalShortAveragePrices(address _token) external view returns (uint256);
    function maxGlobalShortSizes(address _token) external view returns (uint256);
    function tokenDecimals(address _token) external view returns (uint256);
    function tokenWeights(address _token) external view returns (uint256);
    function guaranteedUsd(address _token) external view returns (uint256);
    function poolAmounts(address _token) external view returns (uint256);
    function bufferAmounts(address _token) external view returns (uint256);
    function reservedAmounts(address _token) external view returns (uint256);
    function usdgAmounts(address _token) external view returns (uint256);
    function maxUsdgAmounts(address _token) external view returns (uint256);
    function getRedemptionAmount(address _token, uint256 _usdgAmount) external view returns (uint256);
    function getMaxPrice(address _token) external view returns (uint256);
    function getMinPrice(address _token) external view returns (uint256);

    function getDelta(address _indexToken, uint256 _size, uint256 _averagePrice, bool _isLong, uint256 _lastIncreasedTime) external view returns (bool, uint256);
    function getPosition(address _account, address _collateralToken, address _indexToken, bool _isLong) external view returns (uint256, uint256, uint256, uint256, uint256, uint256, bool, uint256);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;


abstract contract Guarded {

    mapping (address => uint256) public guards;
    function rely(address usr) external auth { guards[usr] = 1; }
    function deny(address usr) external auth { guards[usr] = 0; }

    modifier auth {
        require(guards[msg.sender] == 1, "Guarded/not-authorized");
        _;
    }

    // Use this in actor
    function __RelyOnSender() internal { 
        guards[msg.sender] = 1; 
    }
}

// SPDX-License-Identifier: MIT

pragma solidity 0.8.12;

interface IRewardRouterV2 {
    function weth() external returns(address);
    function gmx() external returns(address);
    function esGmx() external returns(address);
    function bnGmx() external returns(address);
    function glp() external returns(address);
    function stakedGmxTracker() external returns(address);
    function bonusGmxTracker() external returns(address);
    function feeGmxTracker() external returns(address);
    function stakedGlpTracker() external returns(address);
    function feeGlpTracker() external returns(address);
    function glpManager() external returns(address);
    function gmxVester() external returns(address);
    function glpVester() external returns(address);
    function pendingReceivers(address _asset) external returns(address);
    function stakeGmx(uint256 _amount) external;
    function stakeEsGmx(uint256 _amount) external;
    function unstakeGmx(uint256 _amount) external;
    function unstakeEsGmx(uint256 _amount) external;
    function mintAndStakeGlp(address _token, uint256 _amount, uint256 _minUsdg, uint256 _minGlp) external returns (uint256);
    function mintAndStakeGlpETH(uint256 _minUsdg, uint256 _minGlp) external payable returns (uint256);
    function unstakeAndRedeemGlp(address _tokenOut, uint256 _glpAmount, uint256 _minOut, address _receiver) external returns (uint256);
    function unstakeAndRedeemGlpETH(uint256 _glpAmount, uint256 _minOut, address payable _receiver) external returns (uint256);
    function claim() external; // claim to msg.sender
    function claimEsGmx() external;
    function claimFees() external;
    function compound() external;
    function handleRewards(
        bool _shouldClaimGmx,
        bool _shouldStakeGmx,
        bool _shouldClaimEsGmx,
        bool _shouldStakeEsGmx,
        bool _shouldStakeMultiplierPoints,
        bool _shouldClaimWeth,
        bool _shouldConvertWethToEth
    ) external;
    function signalTransfer(address _receiver) external;
    function acceptTransfer(address _sender) external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.12;

import "./integrations/stargate/ISgBridge.sol";
import "./integrations/layerzero/ILayerZeroReceiver.sol";
import "./integrations/layerzero/ILayerZeroEndpoint.sol";
import "./integrations/layerzero/ILayerZeroUserApplicationConfig.sol";
import "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

abstract contract SmartLZBase is OwnableUpgradeable, ILayerZeroReceiver, ILayerZeroUserApplicationConfig  {

    /// lz native endpoint address ID,e.g.
    /// https://layerzero.gitbook.io/docs/technical-reference/mainnet/supported-chain-ids
    address internal _nativeLZEndpoint;

    /// Mapping from chain ID to endpoint address
    mapping(uint16 => bytes) public remotes;

    /// lz native chain ID, e.g. https://layerzero.gitbook.io/docs/technical-reference/mainnet/supported-chain-ids
    uint16 internal _nativeChain;

    /// address of native stargateRouter for swap
    /// https://stargateprotocol.gitbook.io/stargate/interfaces/evm-solidity-interfaces/istargaterouter.sol
    /// https://stargateprotocol.gitbook.io/stargate/developers/contract-addresses/mainnet
    address internal  _sgBridge;

    /// address of ActionPool
    address internal _actionPool;

    event Bridged(uint16 indexed receiverLZId, address indexed receiverAddress, uint256 stableAmount);
    event RouterChanged(address sender, address oldRelayer, address newRelayer);


    modifier onlySelf() {
        require(msg.sender == address(this), "Only self call");
        _;
    }

    /* solhint-disable */
    receive() external payable {
        revert();
    }

    fallback() external payable {
        revert();
    }
    /* solhint-enable */


    /**
    * @notice
    * @param - chainId - target chain id in LayerZero, see more:
    *        (https://layerzero.gitbook.io/docs/technical-reference/testnet/testnet-addresses)
    * @param remoteAddress - RouterAddress in target Chain
    * @dev only deCommas Register Address
    */ // todo - DELETE in production version
    function setRemote(uint16 chainId, bytes memory remoteAddress) public {
        remotes[chainId] = remoteAddress;
    }


    function _setBridge(bytes memory actionData) internal {
        (address newSgBridge) = abi.decode(actionData, (address));
        _sgBridge = newSgBridge;
    }


    // todo only ActionPool
    function setNativeLZEndpoint(address  _endpoint) public /*  onlySelf() */ {
        _nativeLZEndpoint = _endpoint;
    }


    function getNativeEndpoint() public view returns(address) {
        return _nativeLZEndpoint;
    }

    function getNativeId() public view returns(uint16) {
        return _nativeChain;
    }


    function _bridge(bytes memory _actionData) internal {
        (address nativeStableToken,
        uint256 stableAmount,
        uint16 receiverLZId,
        address receiverAddress,
        address destinationStableToken
        ) = abi.decode(_actionData, (address, uint256, uint16, address, address));

        IERC20(nativeStableToken).approve(_sgBridge, stableAmount);
        ISgBridge(_sgBridge).bridge{value: address(this).balance}(nativeStableToken,
            stableAmount,
            receiverLZId,
            receiverAddress,
            destinationStableToken
        );
        emit Bridged(receiverLZId, receiverAddress, stableAmount);
    }


    function _addressToBytes(address a) internal pure returns (bytes memory b) {
        assembly {
            let m := mload(0x40)
            a := and(a, 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF)
            mstore(add(m, 20), xor(0x140000000000000000000000000000000000000000, a))
            mstore(0x40, add(m, 52))
            b := m
        }
    }


    /* solhint-disable */
    /////////// LZ SPECIAL FUNC \\\\\\\\\\\\\\
    /**
    * @notice LZ Config
    * @dev special lz functionality
    */
     function setSendVersion(uint16 version) external override {
        ILayerZeroEndpoint(_nativeLZEndpoint).setSendVersion(version);
    }

    /**
    * @notice LZ Config
    * @dev special lz functionality
    */
    function setReceiveVersion(uint16 version) external override {
        ILayerZeroEndpoint(_nativeLZEndpoint).setReceiveVersion(version);
    }


    /**
    * @notice LZ Config - set the inbound block confirmations
    * @dev special lz functionality
    */
    function setInboundConfirmations(uint16 remoteChainId, uint16 confirmations) external onlyOwner {
        ILayerZeroEndpoint(_nativeLZEndpoint).setConfig(
            ILayerZeroEndpoint(_nativeLZEndpoint).getSendVersion(address(this)),
            remoteChainId,
            2, // CONFIG_TYPE_INBOUND_BLOCK_CONFIRMATIONS
            abi.encode(confirmations)
        );
    }

    /**
    * @notice LZ Config set outbound block confirmations
    * @dev special lz functionality
    */
    function setOutboundConfirmations(uint16 remoteChainId, uint16 confirmations) external onlyOwner {
        ILayerZeroEndpoint(_nativeLZEndpoint).setConfig(
            ILayerZeroEndpoint(_nativeLZEndpoint).getSendVersion(address(this)),
            remoteChainId,
            5, // CONFIG_TYPE_OUTBOUND_BLOCK_CONFIRMATIONS
            abi.encode(confirmations)
        );
    }

    /**
    * @notice LZ Config
    * @dev special lz functionality
    */
    function setConfig(uint16 /*_version*/,
        uint16 _chainId,
        uint _configType,
        bytes calldata _config) external override {
        ILayerZeroEndpoint(_nativeLZEndpoint).setConfig(ILayerZeroEndpoint(_nativeLZEndpoint).getSendVersion(
                address(this)),
            _chainId,
            _configType,
            _config);
    }

    /**
    * @notice LZ Config
    * @dev special lz functionality
    */
     function forceResumeReceive(uint16 _srcChainId, bytes calldata _srcAddress) external override {
        ILayerZeroEndpoint(_nativeLZEndpoint).forceResumeReceive(_srcChainId, _srcAddress);
    }

    /**
    * @notice LZ Config
    * @dev special lz functionality
    */
    function getSendVersion() external view returns (uint16) {
        return ILayerZeroEndpoint(_nativeLZEndpoint).getSendVersion(address(this));
    }

    /**
    * @notice LZ Config
    * @dev special lz functionality
    */
    function getReceiveVersion() external view returns (uint16) {
        return ILayerZeroEndpoint(_nativeLZEndpoint).getReceiveVersion(address(this));
    }

    /**
    * @notice LZ Config
    * @dev special lz functionality
    */
    function getConfig(uint16 /*_dstChainId*/,
                        uint16 _chainId,
                        address /*_dst*/,
                        uint _configType) external view returns (bytes memory) {
        return ILayerZeroEndpoint(_nativeLZEndpoint).getConfig(ILayerZeroEndpoint(_nativeLZEndpoint).getSendVersion(
                                                                                                    address(this)),
                                                                                                    _chainId,
                                                                                                    address(this),
                                                                                                    _configType);
    }
    /* solhint-enable */
 }

// SPDX-License-Identifier: BUSL-1.1

pragma solidity ^0.8.0;

interface ILayerZeroReceiver {
    // @notice LayerZero endpoint will invoke this function to deliver the message on the destination
    // @param _srcChainId - the source endpoint identifier
    // @param _srcAddress - the source sending contract address from the source chain
    // @param _nonce - the ordered message nonce
    // @param _payload - the signed payload is the UA bytes has encoded to be sent
    function lzReceive(uint16 _srcChainId, bytes calldata _srcAddress, uint64 _nonce, bytes calldata _payload) external;
}

// SPDX-License-Identifier: BUSL-1.1

pragma solidity ^0.8.0;

import "./ILayerZeroUserApplicationConfig.sol";

interface ILayerZeroEndpoint is ILayerZeroUserApplicationConfig {
    // @notice send a LayerZero message to the specified address at a LayerZero endpoint.
    // @param _dstChainId - the destination chain identifier
    // @param _destination - the address on destination chain (in bytes). address length/format may vary by chains
    // @param _payload - a custom bytes payload to send to the destination contract
    // @param _refundAddress - if the source transaction is cheaper than the amount of value passed, refund the additional amount to this address
    // @param _zroPaymentAddress - the address of the ZRO token holder who would pay for the transaction
    // @param _adapterParams - parameters for custom functionality. e.g. receive airdropped native gas from the relayer on destination
    function send(uint16 _dstChainId, bytes calldata _destination, bytes calldata _payload, address payable _refundAddress, address _zroPaymentAddress, bytes calldata _adapterParams) external payable;

    // @notice used by the messaging library to publish verified payload
    // @param _srcChainId - the source chain identifier
    // @param _srcAddress - the source contract (as bytes) at the source chain
    // @param _dstAddress - the address on destination chain
    // @param _nonce - the unbound message ordering nonce
    // @param _gasLimit - the gas limit for external contract execution
    // @param _payload - verified payload to send to the destination contract
    function receivePayload(uint16 _srcChainId, bytes calldata _srcAddress, address _dstAddress, uint64 _nonce, uint _gasLimit, bytes calldata _payload) external;

    // @notice get the inboundNonce of a receiver from a source chain which could be EVM or non-EVM chain
    // @param _srcChainId - the source chain identifier
    // @param _srcAddress - the source chain contract address
    function getInboundNonce(uint16 _srcChainId, bytes calldata _srcAddress) external view returns (uint64);

    // @notice get the outboundNonce from this source chain which, consequently, is always an EVM
    // @param _srcAddress - the source chain contract address
    function getOutboundNonce(uint16 _dstChainId, address _srcAddress) external view returns (uint64);

    // @notice gets a quote in source native gas, for the amount that send() requires to pay for message delivery
    // @param _dstChainId - the destination chain identifier
    // @param _userApplication - the user app address on this EVM chain
    // @param _payload - the custom message to send over LayerZero
    // @param _payInZRO - if false, user app pays the protocol fee in native token
    // @param _adapterParam - parameters for the adapter service, e.g. send some dust native token to dstChain
    function estimateFees(uint16 _dstChainId, address _userApplication, bytes calldata _payload, bool _payInZRO, bytes calldata _adapterParam) external view returns (uint nativeFee, uint zroFee);

    // @notice get this Endpoint's immutable source identifier
    function getChainId() external view returns (uint16);

    // @notice the interface to retry failed message on this Endpoint destination
    // @param _srcChainId - the source chain identifier
    // @param _srcAddress - the source chain contract address
    // @param _payload - the payload to be retried
    function retryPayload(uint16 _srcChainId, bytes calldata _srcAddress, bytes calldata _payload) external;

    // @notice query if any STORED payload (message blocking) at the endpoint.
    // @param _srcChainId - the source chain identifier
    // @param _srcAddress - the source chain contract address
    function hasStoredPayload(uint16 _srcChainId, bytes calldata _srcAddress) external view returns (bool);

    // @notice query if the _libraryAddress is valid for sending msgs.
    // @param _userApplication - the user app address on this EVM chain
    function getSendLibraryAddress(address _userApplication) external view returns (address);

    // @notice query if the _libraryAddress is valid for receiving msgs.
    // @param _userApplication - the user app address on this EVM chain
    function getReceiveLibraryAddress(address _userApplication) external view returns (address);

    // @notice query if the non-reentrancy guard for send() is on
    // @return true if the guard is on. false otherwise
    function isSendingPayload() external view returns (bool);

    // @notice query if the non-reentrancy guard for receive() is on
    // @return true if the guard is on. false otherwise
    function isReceivingPayload() external view returns (bool);

    // @notice get the configuration of the LayerZero messaging library of the specified version
    // @param _version - messaging library version
    // @param _chainId - the chainId for the pending config change
    // @param _userApplication - the contract address of the user application
    // @param _configType - type of configuration. every messaging library has its own convention.
    function getConfig(uint16 _version, uint16 _chainId, address _userApplication, uint _configType) external view returns (bytes memory);

    // @notice get the send() LayerZero messaging library version
    // @param _userApplication - the contract address of the user application
    function getSendVersion(address _userApplication) external view returns (uint16);

    // @notice get the lzReceive() LayerZero messaging library version
    // @param _userApplication - the contract address of the user application
    function getReceiveVersion(address _userApplication) external view returns (uint16);
}

// SPDX-License-Identifier: BUSL-1.1

pragma solidity ^0.8.0;

interface ILayerZeroUserApplicationConfig {
    // @notice set the configuration of the LayerZero messaging library of the specified version
    // @param _version - messaging library version
    // @param _chainId - the chainId for the pending config change
    // @param _configType - type of configuration. every messaging library has its own convention.
    // @param _config - configuration in the bytes. can encode arbitrary content.
    function setConfig(uint16 _version, uint16 _chainId, uint _configType, bytes calldata _config) external;

    // @notice set the send() LayerZero messaging library version to _version
    // @param _version - new messaging library version
    function setSendVersion(uint16 _version) external;

    // @notice set the lzReceive() LayerZero messaging library version to _version
    // @param _version - new messaging library version
    function setReceiveVersion(uint16 _version) external;

    // @notice Only when the UA needs to resume the message flow in blocking mode and clear the stored payload
    // @param _srcChainId - the chainId of the source chain
    // @param _srcAddress - the contract address of the source contract at the source chain
    function forceResumeReceive(uint16 _srcChainId, bytes calldata _srcAddress) external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface ISgBridge  {

    function bridge(address token,
        uint256 amount,
        uint16 destChainId,
        address destinationAddress,
        address destinationToken) external payable;


    function swap(
        address tokenA,
        address tokenB,
        uint256 amountA,
        address recipient
    ) external returns (bool, uint256);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (access/Ownable.sol)

pragma solidity ^0.8.0;

import "../utils/ContextUpgradeable.sol";
import "../proxy/utils/Initializable.sol";

/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract OwnableUpgradeable is Initializable, ContextUpgradeable {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    function __Ownable_init() internal onlyInitializing {
        __Ownable_init_unchained();
    }

    function __Ownable_init_unchained() internal onlyInitializing {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        _checkOwner();
        _;
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if the sender is not the owner.
     */
    function _checkOwner() internal view virtual {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }

    /**
     * @dev This empty reserved space is put in place to allow future versions to add new
     * variables without shifting down storage in the inheritance chain.
     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps
     */
    uint256[49] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;
import "../proxy/utils/Initializable.sol";

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract ContextUpgradeable is Initializable {
    function __Context_init() internal onlyInitializing {
    }

    function __Context_init_unchained() internal onlyInitializing {
    }
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }

    /**
     * @dev This empty reserved space is put in place to allow future versions to add new
     * variables without shifting down storage in the inheritance chain.
     * See https://docs.openzeppelin.com/contracts/4.x/upgradeable#storage_gaps
     */
    uint256[50] private __gap;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (proxy/utils/Initializable.sol)

pragma solidity ^0.8.2;

import "../../utils/AddressUpgradeable.sol";

/**
 * @dev This is a base contract to aid in writing upgradeable contracts, or any kind of contract that will be deployed
 * behind a proxy. Since proxied contracts do not make use of a constructor, it's common to move constructor logic to an
 * external initializer function, usually called `initialize`. It then becomes necessary to protect this initializer
 * function so it can only be called once. The {initializer} modifier provided by this contract will have this effect.
 *
 * The initialization functions use a version number. Once a version number is used, it is consumed and cannot be
 * reused. This mechanism prevents re-execution of each "step" but allows the creation of new initialization steps in
 * case an upgrade adds a module that needs to be initialized.
 *
 * For example:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * contract MyToken is ERC20Upgradeable {
 *     function initialize() initializer public {
 *         __ERC20_init("MyToken", "MTK");
 *     }
 * }
 * contract MyTokenV2 is MyToken, ERC20PermitUpgradeable {
 *     function initializeV2() reinitializer(2) public {
 *         __ERC20Permit_init("MyToken");
 *     }
 * }
 * ```
 *
 * TIP: To avoid leaving the proxy in an uninitialized state, the initializer function should be called as early as
 * possible by providing the encoded function call as the `_data` argument to {ERC1967Proxy-constructor}.
 *
 * CAUTION: When used with inheritance, manual care must be taken to not invoke a parent initializer twice, or to ensure
 * that all initializers are idempotent. This is not verified automatically as constructors are by Solidity.
 *
 * [CAUTION]
 * ====
 * Avoid leaving a contract uninitialized.
 *
 * An uninitialized contract can be taken over by an attacker. This applies to both a proxy and its implementation
 * contract, which may impact the proxy. To prevent the implementation contract from being used, you should invoke
 * the {_disableInitializers} function in the constructor to automatically lock it when it is deployed:
 *
 * [.hljs-theme-light.nopadding]
 * ```
 * /// @custom:oz-upgrades-unsafe-allow constructor
 * constructor() {
 *     _disableInitializers();
 * }
 * ```
 * ====
 */
abstract contract Initializable {
    /**
     * @dev Indicates that the contract has been initialized.
     * @custom:oz-retyped-from bool
     */
    uint8 private _initialized;

    /**
     * @dev Indicates that the contract is in the process of being initialized.
     */
    bool private _initializing;

    /**
     * @dev Triggered when the contract has been initialized or reinitialized.
     */
    event Initialized(uint8 version);

    /**
     * @dev A modifier that defines a protected initializer function that can be invoked at most once. In its scope,
     * `onlyInitializing` functions can be used to initialize parent contracts. Equivalent to `reinitializer(1)`.
     */
    modifier initializer() {
        bool isTopLevelCall = !_initializing;
        require(
            (isTopLevelCall && _initialized < 1) || (!AddressUpgradeable.isContract(address(this)) && _initialized == 1),
            "Initializable: contract is already initialized"
        );
        _initialized = 1;
        if (isTopLevelCall) {
            _initializing = true;
        }
        _;
        if (isTopLevelCall) {
            _initializing = false;
            emit Initialized(1);
        }
    }

    /**
     * @dev A modifier that defines a protected reinitializer function that can be invoked at most once, and only if the
     * contract hasn't been initialized to a greater version before. In its scope, `onlyInitializing` functions can be
     * used to initialize parent contracts.
     *
     * `initializer` is equivalent to `reinitializer(1)`, so a reinitializer may be used after the original
     * initialization step. This is essential to configure modules that are added through upgrades and that require
     * initialization.
     *
     * Note that versions can jump in increments greater than 1; this implies that if multiple reinitializers coexist in
     * a contract, executing them in the right order is up to the developer or operator.
     */
    modifier reinitializer(uint8 version) {
        require(!_initializing && _initialized < version, "Initializable: contract is already initialized");
        _initialized = version;
        _initializing = true;
        _;
        _initializing = false;
        emit Initialized(version);
    }

    /**
     * @dev Modifier to protect an initialization function so that it can only be invoked by functions with the
     * {initializer} and {reinitializer} modifiers, directly or indirectly.
     */
    modifier onlyInitializing() {
        require(_initializing, "Initializable: contract is not initializing");
        _;
    }

    /**
     * @dev Locks the contract, preventing any future reinitialization. This cannot be part of an initializer call.
     * Calling this in the constructor of a contract will prevent that contract from being initialized or reinitialized
     * to any version. It is recommended to use this to lock implementation contracts that are designed to be called
     * through proxies.
     */
    function _disableInitializers() internal virtual {
        require(!_initializing, "Initializable: contract is initializing");
        if (_initialized < type(uint8).max) {
            _initialized = type(uint8).max;
            emit Initialized(type(uint8).max);
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library AddressUpgradeable {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCall(target, data, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        require(isContract(target), "Address: call to non-contract");

        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        require(isContract(target), "Address: static call to non-contract");

        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResult(success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verifies that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            // Look for revert reason and bubble it up if present
            if (returndata.length > 0) {
                // The easiest way to bubble the revert reason is using memory via assembly
                /// @solidity memory-safe-assembly
                assembly {
                    let returndata_size := mload(returndata)
                    revert(add(32, returndata), returndata_size)
                }
            } else {
                revert(errorMessage);
            }
        }
    }
}